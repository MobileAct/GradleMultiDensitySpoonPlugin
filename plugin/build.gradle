plugins {
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'kotlin'
apply plugin: 'java-library'
apply plugin: "maven-publish"
apply plugin: 'java-gradle-plugin'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "com.android.tools.build:gradle:3.4.1"
    implementation "com.android.tools.ddms:ddmlib:26.4.2"
    implementation "com.jaredsburrows:gradle-spoon-plugin:1.5.0"
}

sourceCompatibility = "6"
targetCompatibility = "6"

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.6"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.6"
    }
}

test { useJUnitPlatform() }

String versionTag = project.hasProperty("versionTag") ? project.property("versionTag") : ""

bintray {
    user = project.hasProperty("BINTRAY_USER") ? project.property("BINTRAY_USER") : ""
    key = project.hasProperty("BINTRAY_KEY") ? project.property("BINTRAY_KEY") : ""
    publications = ["bintray"]
    publish = true
    pkg {
        repo = "GradleMultiDensitySpoonPlugin"
        name = "GradleMultiDensitySpoonPlugin"
        userOrg = "mobile-act"
        licenses = ["MIT"]
        githubRepo = "MobileAct/GradleMultiDensitySpoonPlugin"
        version {
            name = versionTag.replaceAll("v-", "")
            desc = "Gradle Spoon Plugin Extension for multi density"
            vcsTag = versionTag

        }
    }
}

publishing {
    publications {
        bintray(MavenPublication) {
            from components.java
            groupId "mobile-act"
            artifactId "gradle-multi-density-spoon-plugin"
            version versionTag.replaceAll("v-", "")
        }
    }
}

bintrayUpload.dependsOn assemble